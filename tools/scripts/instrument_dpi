#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long;

sub usage() {
    print << "END"
Usage: instrument-dpi -m [module] -s [signal] [file]
END
}

my ($opt_module,
    $opt_signal,
    $opt_help,
    $opt_file);
GetOptions("m|module=s" => \$opt_module,
           "s|signal=s" => \$opt_signal,
           "h|?|help"   => \$opt_help)
    or usage() and die "[ERROR] Bad option";

usage() and exit if $opt_help;
usage() and die "[ERROR] Mising required option" if not $opt_module or not $opt_signal;
usage() and die "[ERROR] File not specified" if $#ARGV != 0;

$opt_file = $ARGV[0];
open (my $fh, "<", $opt_file) or die "[ERROR] Unable to open $opt_file";

my $instrument_dpi_readmemh = << "END";
  function dpi_readmemh;
    input string file;
    \$display("[INFO] Loading memory from file:");
    \$display("[INFO]   %s", file);
    \$readmemh(file, $opt_signal);
    \$display("[INFO] Done!");
  endfunction
  export "DPI-C" function dpi_readmemh;
  import "DPI-C" context function void dpi_dummy();
  initial dpi_dummy();
END

my $found_it;
while (<$fh>) {
    print $_;
    if ($_ =~ m/^module $opt_module\(/) { $found_it++; }
    next if not $found_it;

    if ($_ =~ m/ $opt_signal[ ;]/) {
        print $instrument_dpi_readmemh;
        $found_it = 0;
    }
}

close $fh;
